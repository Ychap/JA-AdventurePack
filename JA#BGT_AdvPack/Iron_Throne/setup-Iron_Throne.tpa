//////////////////////
// Iron Throne guard
//////////////////////


COPY ~JA#BGT_AdvPack/Iron_Throne/JA#IRONH.CRE~ ~override~


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Iron_Throne/JA#IRONH.BAF~

EXTEND_TOP ~%BaldursGateDocks_BCS%.BCS~ ~JA#BGT_AdvPack/Iron_Throne/AR8100.BAF~ EVALUATE_BUFFER


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Iron_Throne/ja#ironh.d~


COPY_EXISTING ~%BaldursGateDocks%.ARE~ ~override~
	GET_OFFSET_ARRAY doors ARE_V10_DOORS
	PHP_EACH doors AS i => door BEGIN
		READ_ASCII door+0x20 door_id

		PATCH_IF ~%door_id%~ STR_EQ ~DOOR0611~ BEGIN
			WRITE_ASCII door+0x00 ~DOOR0611~ #32 // Name
			WRITE_LONG door+0x28 (THIS | BIT1) // Flag: Door locked(1)
			WRITE_LONG door+0x8c 100 // Lock difficulty
		END
	END
	// add new actor
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 397
		fj_loc_y          = 642
		fj_dest_x         = 397
		fj_dest_y         = 642
		fj_orientation    = 15
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Iron Throne Guard~
		fj_cre_resref     = ~JA#IRONH~
	END
BUT_ONLY
