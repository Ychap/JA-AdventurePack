//////////////////////////////////
// Aldeth's betrayal and remorse
//////////////////////////////////


COPY ~JA#BGT_AdvPack/Betrayal/JA#DEAGB.CRE~ ~override~
	SAY NAME1 #3224 // ~Deagan~ (string ref is true for BGT and BG:EE)
	SAY NAME2 #3224 // ~Deagan~ (string ref is true for BGT and BG:EE)


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Betrayal/JA#CUDEC.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Betrayal/JA#CUDED.BAF~

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Betrayal/JA#DEAGB.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Betrayal/JA#MGUAR.BAF~


EXTEND_TOP ~%EBaldursGate_BCS%.BCS~ ~JA#BGT_AdvPack/Betrayal/AR7800.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%SWBaldursGate_MerchantLeague_L1_BCS%.BCS~ ~JA#BGT_AdvPack/Betrayal/AR7227.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%SWBaldursGate_MerchantLeague_L2_BCS%.BCS~ ~JA#BGT_AdvPack/Betrayal/AR7228.BAF~ EVALUATE_BUFFER


COPY_EXISTING ~%EBaldursGate_BCS%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~CreateCreature("FFHUNT",\[[0-9]+\.[0-9]+\],\(1?[0-9]\|[NS]?[NS]?[WE]?[WE]?\))~ ~NoAction()~
	END
BUT_ONLY

// Disable despawn of Aldeth in chapter 7
ACTION_IF GAME_IS ~bgt~ BEGIN
	<<<<<<<< ...inlined/Aldeth_despawn_del.baf
	IF
		GlobalGT("Chapter","GLOBAL",6)
		Global("DukeThanks","GLOBAL",1)
		Exists("Aldeth")
		!Dead("Aldeth")
	THEN
		RESPONSE #100
			ActionOverride("Aldeth",DestroySelf())
	END
	>>>>>>>>
	COPY_EXISTING ~%SWBaldursGate_MerchantLeague_L2_BCS%.BCS~ ~override~
		REPLACE_BCS_BLOCK
			~...inlined/Aldeth_despawn_del.baf~
			~JA#BGT_AdvPack/empty~
		ON_MISMATCH
			PATCH_WARN ~WARNING: Something went wrong while patching %SOURCE_FILE%! Trying to replace only Aldeth's despawn in file...~
			DECOMPILE_AND_PATCH BEGIN
				SPRINT textToReplace ~ActionOverride("Aldeth",DestroySelf())~
				COUNT_REGEXP_INSTANCES EXACT_MATCH ~%textToReplace%~ num_matches

				PATCH_IF (num_matches > 1) BEGIN
					PATCH_FAIL ~Found too many matches (%num_matches%) of %textToReplace% in %SOURCE_FILE%!~
				END ELSE BEGIN
					REPLACE_TEXTUALLY EXACT_MATCH ~%textToReplace%~ ~Continue()~
				END
			END
		END
	BUT_ONLY
END
ACTION_IF GAME_IS ~bgee eet~ BEGIN
	COPY_EXISTING ~%SWBaldursGate_MerchantLeague_L2_BCS%.BCS~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY ~\(OR(3)[%WNL%%MNL%%LNL%%TAB% ]+\)Exists("Aldeth")~ ~\1Global("AldethMove","GLOBAL",2)~
			REPLACE_TEXTUALLY EXACT_MATCH ~ApplySpellRES("ohdestbg","Aldeth")~ ~NoAction()~
		END
	BUT_ONLY
END


LAF GET_DLG_SCRIPT
	INT_VAR idx_state = 21
			idx_response = 1
	STR_VAR dlg_file = ~ALDETH.DLG~
	RET state21_weight = idx_state_trigger
		state21_response1_action = response_action
END

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Betrayal/ja#aldeth_betrayal.d~ USING ~JA#BGT_AdvPack/languages/%s/ja#aldeth.tra~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Betrayal/ja#aldeth_reconciliation.d~ USING ~JA#BGT_AdvPack/languages/%s/ja#aldeth.tra~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Betrayal/ja#deagb.d~ USING ~JA#BGT_AdvPack/languages/%s/ja#deagan.tra~


COPY_EXISTING ~BRANDI.CRE~ ~override~
	LPF ADD_CRE_SCRIPT INT_VAR offset_start = SCRIPT_CLASS STR_VAR script_new = ~JA#MGUAR~ END
BUT_ONLY

COPY_EXISTING ~DABRON.CRE~ ~override/JA#DABRO.CRE~
	LPF REMOVE_CRE_SCRIPT STR_VAR script_old = ~DABRON~ END

	WRITE_BYTE 0x270 255 // Allegiance: ENEMY
	WRITE_ASCII DIALOG ~None~ #8
BUT_ONLY

COPY_EXISTING ~MERCHG.CRE~ ~override~
	LPF REMOVE_CRE_SCRIPT STR_VAR script_old = ~SEEENEMY~ END
	LPF REPLACE_CRE_SCRIPT STR_VAR script_old = ~MERCHG~ script_new = ~JA#MGUAR~ END
BUT_ONLY
