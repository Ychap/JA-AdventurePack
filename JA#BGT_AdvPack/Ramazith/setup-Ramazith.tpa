/////////////////////
// Ramazith's Tower
/////////////////////


COPY ~JA#BGT_AdvPack/Ramazith/JA#GOLC1.CRE~ ~override~
	SAY NAME1 @2040
	SAY NAME2 @2040


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Ramazith/JA#CURA1.BAF~


// Spawn creatures in Ramazith's tower when he turns hostile
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L2_BCS%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7239.BAF~ EVALUATE_BUFFER
ACTION_IF NOT (MOD_IS_INSTALLED ~setup-FotD.tp2~ 0) BEGIN
	EXTEND_TOP ~%NBaldursGate_RamazithsTower_L3_BCS%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7240.BAF~ EVALUATE_BUFFER
END
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L4_BCS%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7241.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L5_BCS%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7242.BAF~ EVALUATE_BUFFER

// Hostile Ramazith will start dialogue
EXTEND_BOTTOM ~%NBaldursGate_RamazithsTower_L6_BCS%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7238.BAF~ EVALUATE_BUFFER


// BGT (coord fix)
COPY_EXISTING ~AR7238.BCS~ ~override~ // %NBaldursGate_RamazithsTower_L6%
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("RAMAZI",[0.0],0)~ ~CreateCreature("RAMAZI",[169.147],13)~
	END
IF_EXISTS BUT_ONLY

// BGT (Ramazith moves into his tower when Abela is gone)
<<<<<<<< ...inlined/AR7300_old.baf
IF
	Exists("Ramazith")
	Global("HelpRamazith","GLOBAL",0)
	Global("AbelaTeleport","GLOBAL",1)
THEN
	RESPONSE #100
		ActionOverride("Ramazith",DestroySelf())
END
>>>>>>>>
<<<<<<<< ...inlined/AR7300_new.baf
IF
	Exists("Ramazith")
	OR(2)
		Dead("Abela")
		Global("AbelaTeleport","GLOBAL",1)
THEN
	RESPONSE #100
		ActionOverride("Ramazith",DestroySelf())
		SetGlobal("RamazithMove","GLOBAL",1)
END
>>>>>>>>
COPY_EXISTING ~AR7300.BCS~ ~override~ // %NBaldursGate%
	REPLACE_BCS_BLOCK
		~...inlined/AR7300_old.baf~
		~...inlined/AR7300_new.baf~
	ON_MISMATCH
		PATCH_WARN ~WARNING: Something went wrong while patching %SOURCE_FILE%! Trying to replace each part separately...~
		DECOMPILE_AND_PATCH BEGIN
			REFACTOR_TRIGGER EXACT_MATCH ~Global("HelpRamazith","GLOBAL",0)~ ~True()~ // Same as ~AreaCheck(%NBaldursGate%)~ since it's always true here
			REFACTOR_TRIGGER EXACT_MATCH ~Global("AbelaTeleport","GLOBAL",1)~ ~OR(2) Global("AbelaTeleport","GLOBAL",1) Dead("Abela")~
			REPLACE_TEXTUALLY EXACT_MATCH ~ActionOverride("Ramazith",DestroySelf())~ ~\0 SetGlobal("RamazithMove","GLOBAL",1)~
		END
	END
IF_EXISTS BUT_ONLY

// BGEE/EET (Ramazith moves into his tower when Abela is gone)
COPY_EXISTING ~RAMAZITH.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REFACTOR_TRIGGER EXACT_MATCH ~Global("HelpRamazith","GLOBAL",0)~ ~AreaCheck("%NBaldursGate%")~
	END
BUT_ONLY


LAF GET_DLG_SCRIPT
	INT_VAR idx_state = 15
	STR_VAR dlg_file = ~RAMAZI.DLG~
	RET state15_response0_action = response_action
END

OUTER_SET fallback_state_during_quest = (GAME_IS ~bgt~ ? 22 : 10)

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Ramazith/ja#ramazi.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Ramazith/ja#abela.d~

ADD_JOURNAL EXISTING @18 USING ~JA#BGT_AdvPack/languages/%s/ja#ramazi.tra~


// Make CLCK07.ITM(Nymph Cloak) undroppable - can be obtained other ways
COPY_EXISTING ~ABELA.CRE~ ~override~
	GET_OFFSET_ARRAY items CRE_V10_ITEMS
	PHP_EACH items AS i => item BEGIN
		READ_ASCII item item_filename
		PATCH_IF (~%item_filename%~ STR_EQ ~CLCK07~) BEGIN
			WRITE_LONG item+0x10 (THIS | BIT3) // Flag: Undroppable(3)
		END
	END
BUT_ONLY

// BGT
COPY_EXISTING ~RAGEFA.CRE~ ~override~
	LPF REPLACE_CRE_SCRIPT STR_VAR script_old = ~INITDLG~ script_new = ~SHOUTDLG~ END
BUT_ONLY


COPY_EXISTING ~%NBaldursGate_RamazithsTower_L2%.ARE~ ~override~
	LPF DELETE_AREA_ACTOR STR_VAR cre_to_delete = ~JELLYMU~ END
BUT_ONLY

ACTION_IF NOT (MOD_IS_INSTALLED ~setup-FotD.tp2~ 0) BEGIN
	COPY_EXISTING ~%NBaldursGate_RamazithsTower_L3%.ARE~ ~override~
		LPF DELETE_AREA_ACTOR STR_VAR cre_to_delete = ~GHAST~ END
	BUT_ONLY
END

COPY_EXISTING ~%NBaldursGate_RamazithsTower_L4%.ARE~ ~override~
	LPF DELETE_AREA_ACTOR STR_VAR cre_to_delete = ~HOBELITE~ END
BUT_ONLY

COPY_EXISTING ~%NBaldursGate_RamazithsTower_L5%.ARE~ ~override~
	LPF DELETE_AREA_ACTOR STR_VAR cre_to_delete = ~KOBCOMM~ END
BUT_ONLY


ACTION_IF GAME_IS ~bgt~ BEGIN
	COPY ~JA#BGT_AdvPack/Ramazith/AR7238.WED~ ~override/%NBaldursGate_RamazithsTower_L6%.WED~
	INCLUDE ~%MOD_FOLDER%/lib/alter_searchmap.tpa~
	LAF ALTER_SEARCHMAP
		STR_VAR
		path_to_2da_file = EVAL ~%MOD_FOLDER%/Ramazith/AR7238SR_changes.2da~ 	// full path to the *changes.2da file containing your icons, e.g. ~mymod/bam/AR3700SR_changes_ee.2da~
		areaname = EVAL ~%NBaldursGate_RamazithsTower_L6%~ 						// area name, e.g. ~AR3700~
	END
END
